plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.1'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'de.kaviedes'
version = '0.0.1-SNAPSHOT'
description = 'think-of-you-3'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-websocket'
    implementation 'com.google.firebase:firebase-admin:9.2.0'
    implementation 'io.jsonwebtoken:jjwt-api:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.6'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.testcontainers:junit-jupiter:1.20.4'
    testImplementation 'org.testcontainers:mongodb:1.20.4'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
    useJUnitPlatform()
}

def os = org.gradle.internal.os.OperatingSystem.current()
def npmCmd = os.isWindows() ? 'npm.cmd' : 'npm'
def npxCmd = os.isWindows() ? 'npx.cmd' : 'npx'
def dockerCmd = os.isWindows() ? 'docker.exe' : 'docker'

tasks.register('frontendNpmInstall', Exec) {
    workingDir 'frontend'
    commandLine npmCmd, 'install'
}

tasks.register('frontendPlaywrightInstall', Exec) {
    workingDir 'frontend'
    commandLine npxCmd, 'playwright', 'install'
    dependsOn 'frontendNpmInstall'
}

tasks.register('playwrightTest', Exec) {
    workingDir 'frontend'
    commandLine npmCmd, 'run', 'test:e2e'
    dependsOn 'frontendPlaywrightInstall'
}

tasks.register('composeUp', Exec) {
    commandLine dockerCmd, 'compose', 'up', '--build', '-d'
}

tasks.register('composeDown', Exec) {
    commandLine dockerCmd, 'compose', 'down'
}

tasks.register('e2e') {
    dependsOn 'composeUp', 'playwrightTest'
    finalizedBy 'composeDown'
}
